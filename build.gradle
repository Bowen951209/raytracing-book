plugins {
    id 'java'
    id 'application'
    id 'distribution'
}

group = 'net.bowen'
version = '0.0.2'

repositories {
    mavenCentral()
}

ext {
    lwjglVersion = '3.3.3'
    imguiVersion = '1.87.3'
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    implementation 'commons-cli:commons-cli:1.4'

    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    ['', '-opengl', '-glfw'].each {
        implementation "org.lwjgl:lwjgl$it:$lwjglVersion"
        implementation "org.lwjgl:lwjgl$it::natives-windows"
    }

    implementation "io.github.spair:imgui-java-binding:$imguiVersion"
    implementation "io.github.spair:imgui-java-lwjgl3:$imguiVersion"
    implementation "org.joml:joml:1.10.7"

    implementation "io.github.spair:imgui-java-natives-windows:$imguiVersion"
}

test {
    useJUnitPlatform()
}

application {
    mainClassName = 'net.bowen.Main'
}

sourceSets {
    main {
        resources {
            srcDirs "src/main/resources", "src/main/configs"
        }
    }
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes "Main-Class": "net.bowen.Main"
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}